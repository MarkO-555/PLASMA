include "inc/cmdsys.plh"
include "inc/lines.plh"
include "inc/vblank.plh"
include "dhgr.tk/inc/dhgrlib.plh"
include "dhgr.tk/inc/dhgrutils.plh"

sysflags resxtxt1|reshgr1|resxhgr1
//
// ViewScreen coordinates
//
const VIEW_LEFT7   = 3
const VIEW_RIGHT7  = 17
const VIEW_WIDTH7  = VIEW_RIGHT7 - VIEW_LEFT7
const VIEW_LEFT    = VIEW_LEFT7  * 7 // 21
const VIEW_RIGHT   = VIEW_RIGHT7 * 7 // 119
const VIEW_WIDTH   = VIEW_RIGHT  - VIEW_LEFT
const VIEW_TOP     = 24
const VIEW_BOTTOM  = 122
const VIEW_HEIGHT  = VIEW_BOTTOM - VIEW_TOP
const VIEW_ORGX    = (VIEW_LEFT+VIEW_RIGHT)/2
const VIEW_ORGY    = (VIEW_TOP+VIEW_BOTTOM)/2
//
// Enterprise sprites
//
struc t_sprite
  var spr_xorg
  var spr_yorg
  var spr_width
  var spr_height
  var spr_pixmap
  var spr_bl7
end
res[t_sprite * 8] ncc1701
char[12] sprFile = "ENT.UP.SPR"
char[12]         = "ENT.UR.SPR"
char[12]         = "ENT.RT.SPR"
char[12]         = "ENT.DR.SPR"
char[12]         = "ENT.DN.SPR"
char[12]         = "ENT.DL.SPR"
char[12]         = "ENT.LT.SPR"
char[12]         = "ENT.UL.SPR"
const ENT_W7 = 5
const ENT_H  = 43
const ENT_SZ = ENT_W7*4*ENT_H
//
// Dither matrices
//
const DITHER0   = $00
const DITHER25  = $04
const DITHER50  = $06
const DITHER75  = $07
const DITHER100 = $0F
byte[] dither   = DITHER0, DITHER25, DITHER50, DITHER75, DITHER100
var sin90[] // first 90 degrees of sin in fixed s.15 format
var =     0,   571,  1143,  1714,  2285,  2855,  3425,  3993
var =  4560,  5126,  5690,  6252,  6812,  7371,  7927,  8480
var =  9032,  9580, 10125, 10668, 11207, 11743, 12275, 12803
var = 13327, 13848, 14364, 14876, 15383, 15886, 16383, 16876
var = 17364, 17846, 18323, 18794, 19260, 19720, 20173, 20621
var = 21062, 21497, 21926, 22347, 22762, 23170, 23571, 23964
var = 24351, 24730, 25101, 25465, 25821, 26169, 26509, 26841
var = 27165, 27481, 27788, 28087, 28377, 28659, 28932, 29196
var = 29451, 29697, 29935, 30163, 30381, 30591, 30791, 30982
var = 31164, 31336, 31498, 31651, 31794, 31928, 32051, 32165
var = 32270, 32364, 32449, 32523, 32588, 32643, 32688, 32723
var = 32748, 32763, 32767
//
// Pattern fill
//
const PAT_WIDTH  = SCR_WIDTH
const PAT_HEIGHT = 2
word pattern
//
// Polygon variables
//
word leftedge, rightedge  // Edge arrays
byte polytop,  polybottom // Poly top and bottom extents
byte startvx, startvy     // Starting poly vertex
byte polyvx, polyvy       // Current poly vertex
//
// Random seed
//
var rndseed = $A965

def random#1
  rndseed = (rndseed >> 3) ^ (rndseed << 13) + $4321
  return rndseed
end
//
// Create pattern pixmap for filling
//
def setPattern(clr1, clr2, dither)#0
  word patfill, clr

  if not pattern
    pattern = heapalloc(SCR_WIDTH * PAT_HEIGHT)
  fin
  //
  // Even scanlines
  //
  clr = dither & 1 ?? clr2 :: clr1
  patfill = clr | (clr << 8)
  clr = dither & 2 ?? clr2 :: clr1
  patfill = patfill | (clr << 4) | (clr << 12)
  memset(pattern, patfill, SCR_WIDTH/2)
  patfill = (patfill << 4) | clr // Rotate pattern for odd pixel
  memset(pattern + SCR_WIDTH, patfill, SCR_WIDTH/2)
  //
  // Odd scanlines
  //
  clr = dither & 4 ?? clr2 :: clr1
  patfill = clr | (clr << 8)
  clr = dither & 8 ?? clr2 :: clr1
  patfill = patfill | (clr << 4) | (clr << 12)
  memset(pattern + SCR_WIDTH/2, patfill, SCR_WIDTH/2)
  patfill = (patfill << 4) | clr  // Rotate pattern for odd pixel
  memset(pattern + SCR_WIDTH + SCR_WIDTH/2, patfill, SCR_WIDTH/2)
end
//
// Sin and Cos
//
def sin(deg)#1
  while deg > 360; deg = deg - 360; loop
  while deg < 0; deg = deg + 360; loop
  if deg <= 90
    return sin90[deg]
  elsif deg <= 180
    return sin90[180 - deg]
  elsif deg <= 270
    return -sin90[deg - 180]
  fin
  return -sin90[360 - deg]
end
def cos(deg)#1
  return sin(deg + 90)
end

def patrnRect(left, top, right, bottom)#0
  word patscan
  byte width, y

  width = right - left + 1
  patscan = pattern + (left & 1 ?? SCR_WIDTH :: 0)
  for y = top to bottom
    dcgrPixMap(left, y, width, 1, patscan + (y & 1 ?? (SCR_WIDTH/2) :: 0))
  next
end
def solidRect(left, top, right, bottom)#0
  byte l7, r7, x

  l7 = (left | 7) / 7
  r7 = right / 7
  if l7 < r7
    dhgrClearBl7(l7, top, r7 - l7, bottom - top + 1)
  fin
  l7 = l7 * 7
  if l7 > left
    for x = l7-1 downto left
      dcgrVLin(top, bottom, x)
    next
  fin
  r7 = r7 * 7
  if r7 < right
    for x = r7 to right
      dcgrVLin(top, bottom, x)
    next
  fin
end
//
// Convex polygon
//
def setEdge(x, y)#0
  if x < leftedge->[y];  leftedge->[y]  = x; fin
  if x > rightedge->[y]; rightedge->[y] = x; fin
end
def startPoly(x, y)#0
  setlineplot(@setEdge)
  leftedge  = heapalloc(SCR_HEIGHT)
  rightedge = heapalloc(SCR_HEIGHT)
  memset(leftedge,  $FFFF, SCR_HEIGHT)
  memset(rightedge, $0000, SCR_HEIGHT)
  polytop    = y
  polybottom = y
  startvx    = x
  startvy    = y
  polyvx     = x
  polyvy     = y
end
def addVertex(x, y)#0
  if y < polytop;    polytop    = y; fin
  if y > polybottom; polybottom = y; fin
  line(polyvx, polyvy, x, y)
  polyvx     = x
  polyvy     = y
end
def patrnPoly(x, y)#0
  var width
  byte left

  if y < polytop;    polytop    = y; fin
  if y > polybottom; polybottom = y; fin
  line(polyvx, polyvy, x, y)
  line(x, y, startvx, startvy)
  for y = polytop to polybottom
    left  = leftedge->[y]
    width = rightedge->[y] - left + 1
    if width > 0
      dcgrPixMap(left, y, width, 1, \
                pattern + (left & 1 ?? SCR_WIDTH     :: 0) \
                        + (y    & 1 ?? (SCR_WIDTH/2) :: 0))
    fin
  next
  heaprelease(leftedge)
end
def solidPoly(x, y)#0
  byte left, right

  if y < polytop;    polytop    = y; fin
  if y > polybottom; polybottom = y; fin
  line(polyvx, polyvy, x, y)
  line(x, y, startvx, startvy)
  for y = polytop to polybottom
    left  = leftedge->[y]
    right = rightedge->[y]
    if right >= left
      dcgrHLin(left, right, y)
    fin
  next
  heaprelease(leftedge)
end
//
// Ellipse
//
def solidEllip(xorg, yorg, width, height)#0
  var x, y, angle

  for angle = 0 to 90 step 90 / height
    x = ((cos(angle) >> 8) * width) >> 8
    y = ((sin(angle) >> 8) * height) >> 8
    dcgrHLin(xorg - x, xorg + x, yorg + y)
    dcgrHLin(xorg - x, xorg + x, yorg - y)
  next
end
def patrnEllip(xorg, yorg, width, height)#0
  var x, y, angle, left, len, scan

  for angle = 0 to 90 step 90 / height
    x = ((cos(angle) >> 8) * width) >> 8
    y = ((sin(angle) >> 8) * height) >> 8
    left = xorg - x
    len  = x * 2
    if len == 0; len = 1; fin
    scan = yorg + y
    dcgrPixMap(left, scan, len, 1, \
               pattern + (left & 1 ?? SCR_WIDTH     :: 0) \
                       + (scan & 1 ?? (SCR_WIDTH/2) :: 0))
    scan = yorg - y
    dcgrPixMap(left, scan, len, 1, \
               pattern + (left & 1 ?? SCR_WIDTH     :: 0) \
                       + (scan & 1 ?? (SCR_WIDTH/2) :: 0))
  next
end

def frame(left, top, right, bottom, thickness)#0
  while thickness
    thickness--
    dcgrHLin(left + thickness, right  - thickness, top    + thickness)
    dcgrHLin(left + thickness, right  - thickness, bottom - thickness)
    dcgrVLin(top  + thickness, bottom - thickness, left   + thickness)
    dcgrVLin(top  + thickness, bottom - thickness, right  - thickness)
  loop
end
//
// Draw components
//
def drawSurround#0
  //
  // Viewscreen surround
  //
  dcgrColor(CLR_GREY)
  dhgrClearBl7(0,           0,           SCR_WIDTH7, VIEW_TOP)
  dhgrClearBl7(0,           VIEW_BOTTOM, SCR_WIDTH7, VIEW_TOP)
  dhgrClearBl7(0,           VIEW_TOP,    VIEW_LEFT7, VIEW_HEIGHT)
  dhgrClearBl7(VIEW_RIGHT7, VIEW_TOP,    VIEW_LEFT7, VIEW_HEIGHT)
  setPattern(CLR_BLACK, CLR_GREY, DITHER50)
  patrnRect(0, VIEW_BOTTOM+VIEW_TOP, SCR_WIDTH-1, SCR_HEIGHT-1)
  //
  // Above viewscreen ceiling
  //
  setPattern(CLR_BLACK, CLR_GREY, DITHER50)
  startPoly(0, 0)
  addVertex(SCR_WIDTH-1, 0)
  addVertex(VIEW_RIGHT+3-1, VIEW_TOP-6)
  patrnPoly(VIEW_LEFT-3, VIEW_TOP-6)
  //
  // Below viewscreen bump-out
  //
  setPattern(CLR_GREY, CLR_WHITE, DITHER50)
  startPoly(VIEW_LEFT-3,    VIEW_BOTTOM+5)
  addVertex(VIEW_RIGHT+3-1, VIEW_BOTTOM+5)
  addVertex(SCR_WIDTH-1,    VIEW_BOTTOM+VIEW_TOP-1)
  patrnPoly(0,              VIEW_BOTTOM+VIEW_TOP-1)
  //
  // Viewscreen frame
  //
  dcgrColor(CLR_DRKBLUE)
  frame(VIEW_LEFT-3, VIEW_TOP-3, VIEW_RIGHT+3-1, VIEW_BOTTOM+3-1, 3)
  dcgrColor(CLR_MEDBLUE)
  frame(VIEW_LEFT-2, VIEW_TOP-2, VIEW_RIGHT+2-1, VIEW_BOTTOM+2-1, 1)
end
def drawRails#0
  dcgrColor(CLR_ORANGE)
  dhgrClearBl7(0,     132, 40/7, 1)
  dhgrClearBl7(107/7, 132, 40/7, 1)
  dcgrColor(CLR_MAGENTA)
  dhgrClearBl7(0,     133, 40/7, 7)
  dhgrClearBl7(107/7, 133, 40/7, 7)
  dcgrColor(CLR_BLACK)
  solidRect( 20, 140,  24, 191)
  solidRect(115, 140, 119, 191)
end
def drawAlerts#0
  dcgrColor(CLR_ORANGE)
  frame( 1*7-2, 40-2,  2*7+2-1, 80+2-1, 2)
  frame(18*7-2, 40-2, 19*7+2-1, 80+2-1, 2)
  dcgrColor(CLR_BLACK)
  dhgrClearBl7( 1, 40, 1, 40)
  dhgrClearBl7(18, 40, 1, 40)
end
def drawConsoles#0
  dcgrColor(CLR_MAGENTA)
  startPoly(70-30,          160)
  addVertex(70-30, SCR_HEIGHT-1)
  solidPoly(70-40, SCR_HEIGHT-1)
  startPoly(70+30-1,          160)
  addVertex(70+30-1, SCR_HEIGHT-1)
  solidPoly(70+40-1, SCR_HEIGHT-1)
  setPattern(CLR_BLACK, CLR_BROWN, DITHER50)
  startPoly(0,              160)
  addVertex(70-30,          160)
  addVertex(70-40, SCR_HEIGHT-1)
  patrnPoly(0,     SCR_HEIGHT-1)
  startPoly(70+30-1,              160)
  addVertex(SCR_WIDTH-1,          160)
  addVertex(SCR_WIDTH-1, SCR_HEIGHT-1)
  patrnPoly(70+40-1,     SCR_HEIGHT-1)
  dcgrColor(CLR_BLACK)
  startPoly(0,     170)
  addVertex(70-40, 170)
  addVertex(70-51, SCR_HEIGHT-1)
  solidPoly(0,     SCR_HEIGHT-1)
  startPoly(70+40-1,              170)
  addVertex(SCR_WIDTH-1,          170)
  addVertex(SCR_WIDTH-1, SCR_HEIGHT-1)
  solidPoly(70+51-1,     SCR_HEIGHT-1)
end
def drawNavigation#0
  dcgrColor(CLR_WHITE)
  startPoly(70-20,           150)
  addVertex(70+20-1,         150)
  addVertex(70+30-1, SCR_HEIGHT-1)
  solidPoly(70-30,   SCR_HEIGHT-1)
  setPattern(CLR_GREY, CLR_BROWN, DITHER50)
  patrnEllip(70, 170, 40, 35)
end
//
// Read Enterprise sprites
//
def readEnterprise#0
  var xorg, yorg, width, height, pixptr, t_ptr, f_ptr
  byte i

  t_ptr = @ncc1701
  f_ptr = @sprFile
  for i = 0 to 7
    xorg, yorg, width, height, pixptr = spriteRead(f_ptr)
    t_ptr=>spr_xorg   = xorg
    t_ptr=>spr_yorg   = yorg
    t_ptr=>spr_width  = width
    t_ptr=>spr_height = height
    t_ptr=>spr_pixmap = pixptr
    t_ptr=>spr_bl7  = heapalloc(ENT_SZ)
    dhgrSurfMem(OP_SRC, ENT_H, t_ptr=>spr_bl7, ENT_W7*4)
    dcgrColor(CLR_BLACK)
    dhgrClearBl7(0, 0, ENT_W7, ENT_H)
    dcgrPixmap(ENT_W7*7/2 - xorg,ENT_H/2 - yorg, width, height, pixptr)
    t_ptr = t_ptr + t_sprite
    f_ptr = f_ptr + 12
  next
end
//
// Animate stuff
//
def keypressed#1
  return ^$C000 & $80
end
def idleViewPlot(x)#0
  x = (x&63)+70-32
  dcgrHLin(x - 1, x + 1, VIEW_BOTTOM+3)
  dcgrHlin(x - 1, x + 1, VIEW_BOTTOM+4)
end
def newStar#3
  return 15 - (random & 30), 15 - (random & 30), random & 15
end
def idleStars#0
  byte xpos, star, nstars
  var starx, stary, starincx, starincy, starclr
  var x, y, incx, incy

  waitVBL; nstars = waitVBL / 50
  starx    = heapalloc(nstars*2)
  stary    = heapalloc(nstars*2)
  starincx = heapalloc(nstars*2)
  starincy = heapalloc(nstars*2)
  starclr  = heapalloc(nstars)
  nstars--
  for star = 0 to nstars
    repeat
      incx, incy, starclr->[star] = newstar
    until incx <> 0 and incy <> 0
    if starclr->[star] == 0; starclr->[star] = 15; fin
    starx=>[star]    = incx * (VIEW_WIDTH-1)/2
    stary=>[star]    = incy * (VIEW_HEIGHT-1)/2
    starincx=>[star] = incx
    starincy=>[star] = incy
  next
  xpos = 8
  star = 0
  while not keypressed
    dcgrColor(CLR_WHITE)
    idleViewPlot(xpos)
    dcgrColor(CLR_YELLOW)
    idleViewPlot(xpos-4)
    dcgrColor(CLR_ORANGE)
    idleViewPlot(xpos-8)
    dcgrColor(CLR_GREY)
    idleViewPlot(xpos-12)
    xpos = xpos + 4
    for star = 0 to nstars
      x = starx=>[star]
      y = stary=>[star]
      incx = x + starincx=>[star]
      incy = y + starincy=>[star]
      if  incx > (VIEW_WIDTH-1)*8  or incx < -VIEW_WIDTH*8 \
       or incy > (VIEW_HEIGHT-1)*8 or incy < -VIEW_HEIGHT*8
        repeat
          incx, incy, starclr->[star] = newStar
        until incx <> 0 and incy <> 0
        if starclr->[star] == 0; starclr->[star] = 15; fin
        starx=>[star]    = incx << 5
        stary=>[star]    = incy << 5
        starincx=>[star] = incx
        starincy=>[star] = incy
      else
        starx=>[star]    = incx
        stary=>[star]    = incy
        incx = starincx=>[star]
        incy = starincy=>[star]
        starincx=>[star] = incx + ((incx > 0 ?? incx + 7 :: incx - 7) >> 3)
        starincy=>[star] = incy + ((incy > 0 ?? incy + 7 :: incy - 7) >> 3)
      fin
      dcgrColor(CLR_BLACK)
      dcgrPixel((x >> 4) + VIEW_ORGX, (y >> 4) + VIEW_ORGY)
      dcgrColor(starclr->[star])
      dcgrPixel((starx=>[star] >> 4) + VIEW_ORGX, (stary=>[star] >> 4) + VIEW_ORGY)
    next
  loop
  getc
  heaprelease(starx)
end
def idleSpin#0
  var viewbuf, viewspan
  var i, k, oi, oj
  byte xpos

  //
  // Use back buffer
  //
  viewbuf, viewspan = dhgrAllocBl7Mem(VIEW_WIDTH, VIEW_HEIGHT)
  i    = @ncc1701
  k    = 0
  xpos = 0
  while not keypressed
    //
    // Rotate Enterprise
    //
    dhgrSurfMem(OP_SRC, VIEW_HEIGHT, viewbuf, viewspan)
    dcgrColor(CLR_BLACK)
    dhgrClearBl7(0, 0, VIEW_WIDTH7, VIEW_HEIGHT)
    dcgrColor(CLR_GREEN)
    for oi = (xpos/4)&31 to VIEW_WIDTH-1 step 32
      dcgrVLin(0, VIEW_HEIGHT-1, oi)
    next
    for oi = (xpos/4)&63 to VIEW_HEIGHT-1 step 64
      dcgrHLin(0, VIEW_WIDTH-1, oi)
    next
    dhgrOp(OP_OR)
    dhgrMemBl7((VIEW_WIDTH7 - ENT_W7 + 1)/2, (VIEW_HEIGHT-ENT_H + 1)/2, \
               ENT_W7, ENT_H, \
               i=>spr_bl7, ENT_W7*4)
    dhgrSurfScr(OP_SRC)
    waitVBL
    dhgrMemBl7(VIEW_LEFT7, VIEW_TOP, \
               VIEW_WIDTH7, VIEW_HEIGHT, \
               viewbuf, viewspan)
    i = i + t_sprite
    k++
    if k > 7
      k = 0
      i = @ncc1701
    fin
    //
    // Animate useless view screen lights
    //
    dcgrColor(CLR_WHITE)
    idleViewPlot(xpos)
    dcgrColor(CLR_YELLOW)
    idleViewPlot(xpos-4)
    dcgrColor(CLR_ORANGE)
    idleViewPlot(xpos-8)
    dcgrColor(CLR_GREY)
    idleViewPlot(xpos-12)
    xpos = xpos + 4
  loop
  getc
end
//
// Read sprites
//
readEnterprise
//
// Initialize
//
dhgrMode(DHGR_COLOR_MODE)
//
// Draw bridge
//
drawSurround
drawRails
drawAlerts
drawConsoles
drawNavigation
//
// Animate stars waiting for keypress
//
idleStars
idleSpin
//
// All done
//
dhgrMode(DHGR_TEXT_MODE)
done
