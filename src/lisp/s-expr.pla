include "inc/cmdsys.plh"
include "inc/int32.plh"
include "inc/fpstr.plh"

const TYPE_MASK  = $70
const NIL        = $00
const BOOL_FALSE = $00
const BOOL_TRUE  = $01
const CONS_TYPE  = $10
const SYM_TYPE   = $20
const SYM_LEN    = $0F
const NUM_TYPE   = $30
const NUM_INT    = $31
const NUM_FLOAT  = $32
const ARRAY_TYPE = $40
const MARK_BIT   = $80
const MARK_MASK  = $7F
const NULL_HACK  = 1  // Hack so we can set elements to NULL

struc t_elem
  word link
  byte type
end
struc t_cons
  res[t_elem]
  word car
  word cdr
end
struc t_sym
  res[t_elem]
  word natv
  word lambda
  word array
  word apval
  char name[0]
end
struc t_numint
  res[t_elem]
  word intval[2]
end
struc t_numfloat
  res[t_elem]
  res floatval[10]
end
struc t_array
  res[t_elem]
  word dimension[4]
  word offset[4]
  word arraysize
  word arraymem
end

byte trace = FALSE

const fmt_fp          = FPSTR_FIXED|FPSTR_STRIP|FPSTR_FLEX
export var fmt_fpint  = 6
export var fmt_fpfrac = 4

byte prhex = FALSE // Hex output flag for integers

var assoc_list  = NULL // SYM->value association list
var cons_list   = NULL
var cons_free   = NULL
var int_list    = NULL
var int_free    = NULL
var float_list  = NULL
var float_free  = NULL
var sym_list    = NULL

var sym_nil, sym_quote, sym_lambda, sym_funarg, sym_set
var sym_cond, sym_if, sym_label, sym_for, sym_space, sym_cr
res[t_elem] pred_true  = 0, 0, BOOL_TRUE
predef print_expr(expr)#0
predef eval_expr(expr)#1

//
// Garbage collector
// - note, anytime eval_expr is called there is the possibility of
//   garbage collecting. If there are any in-flight elements (cons,
//   int, float) they may be collected and returned to the free list.
//   Use the sweep_stack to temporarily keep a reference to these elements.
//

const SWEEPSTACK_MAX  = 64
byte  sweep_stack_top = 0
var   sweep_stack[SWEEPSTACK_MAX] // In-flight expressions

const GC_TRIGGER = 50
byte gc_pull     = 0

def mark_list(list)#0
  while list
    list->type = list->type | MARK_BIT
    list = list=>link
  loop
end

def mark_elems#0
  mark_list(cons_list)
  mark_list(int_list)
  mark_list(float_list)
end

def sweep_expr(expr)#0
  while expr
    if not expr->type & MARK_BIT; return; fin // Stop if MARK_BIT clear
    expr->type = expr->type & MARK_MASK
    if expr->type == CONS_TYPE
      sweep_expr(expr=>car)
      expr = expr=>cdr
    else
      expr = NULL
    fin
  loop
end

def sweep_used#0
  var symptr, i, memptr, size

  sweep_expr(assoc_list)
  symptr = sym_list
  while symptr
    //
    // Sweep symbol properties
    //
    if symptr=>apval
      sweep_expr(symptr=>apval ^ NULL_HACK)
    elsif symptr=>lambda
      sweep_expr(symptr=>lambda)
    elsif symptr=>array
      memptr = symptr=>array=>arraymem
      size   = symptr=>array=>arraysize - 1
      for i = 0 to size
        sweep_expr(memptr=>[i])
      next
    fin
    symptr = symptr=>link
  loop
  if sweep_stack_top
    //
    // Sweep in-flight parameters
    //
    for i = 0 to sweep_stack_top - 1
      sweep_expr(sweep_stack[i])
    next
  fin
end

def push_sweep_stack(expr)#1
  if sweep_stack_top == SWEEPSTACK_MAX - 1
    puts("Sweep stack overflow\n")
    return NULL
  fin
  sweep_stack[sweep_stack_top] = expr
  sweep_stack_top++
  return expr
end

def pop_sweep_stack#1
  if sweep_stack_top == 0
    puts("Sweep stack underflow\n")
    return NULL
  fin
  sweep_stack_top--
  return sweep_stack[sweep_stack_top]
end

def collect_list(listhead, freehead)#2
  var elemptr, prevptr

  prevptr = NULL
  elemptr = listhead
  while elemptr
    if elemptr->type & MARK_BIT
      elemptr->type = elemptr->type & MARK_MASK
      //puts("Free: "); print_expr(elemptr); putln
      if prevptr
        prevptr=>link = elemptr=>link
        elemptr=>link = freehead
        freehead      = elemptr
        elemptr       = prevptr=>link
      else
        listhead      = elemptr=>link
        elemptr=>link = freehead
        freehead      = elemptr
        elemptr       = listhead
      fin
    else
      prevptr = elemptr
      elemptr = elemptr=>link
    fin
  loop
  return listhead, freehead
end

def collect_unused#0
  cons_list,  cons_free  = collect_list(cons_list,  cons_free)
  int_list,   int_free   = collect_list(int_list,   int_free)
  float_list, float_free = collect_list(float_list, float_free)
end

export def gc#0
  mark_elems
  sweep_used
  collect_unused
  gc_pull = 0
end

//
// Build ATOMS
//

export def new_cons#1
  var consptr

  if cons_free
    consptr   = cons_free
    cons_free = cons_free=>link
  else
    gc_pull++
    consptr = heapalloc(t_cons)
  fin
  consptr=>link = cons_list
  cons_list     = consptr
  consptr->type = CONS_TYPE
  consptr=>car  = NULL
  consptr=>cdr  = NULL
  return consptr
end

export def new_int(intlo, inthi)#1
  var intptr

  if int_free
    intptr   = int_free
    int_free = int_free=>link
  else
    gc_pull++
    intptr = heapalloc(t_numint)
  fin
  intptr=>link      = int_list
  int_list          = intptr
  intptr->type      = NUM_INT
  intptr=>intval[0] = intlo
  intptr=>intval[1] = inthi
  return intptr
end

export def new_float(extptr)#1
  var floatptr

  if float_free
    floatptr   = float_free
    float_free = float_free=>link
  else
    gc_pull++
    floatptr = heapalloc(t_numfloat)
  fin
  floatptr=>link = float_list
  float_list     = floatptr
  floatptr->type = NUM_FLOAT
  memcpy(floatptr + floatval, extptr, 10)
  return floatptr
end

def new_array(dim0, dim1, dim2, dim3)
  var ofst0, ofst1, ofst2, ofst3
  var size, aptr, memptr

  if dim3
    ofst3 = 2
    ofst2 = dim3  * 2
    ofst1 = ofst2 * dim2
    ofst0 = ofst1 * dim1
  elsif dim2
    ofst2 = 2
    ofst1 = dim2 * 2
    ofst0 = ofst1 * dim1
  elsif dim1
    ofst1 = 2
    ofst0 = dim1 * 2
  else
    ofst0 = 2
  fin
  size = dim0 * ofst0
  memptr = heapalloc(size)
  if not memptr
    puts("Array too large!\n")
    return NULL
  fin
  memset(memptr, NULL, size)
  aptr               = heapalloc(t_array)
  aptr=>link         = NULL
  aptr->type         = ARRAY_TYPE
  aptr=>dimension[0] = dim0
  aptr=>dimension[1] = dim1
  aptr=>dimension[2] = dim2
  aptr=>dimension[3] = dim3
  aptr=>offset[0]    = ofst0
  aptr=>offset[1]    = ofst1
  aptr=>offset[2]    = ofst2
  aptr=>offset[3]    = ofst3
  aptr=>arraysize    = size/2
  aptr=>arraymem     = memptr
  return aptr
end

def match_sym(symstr)
  var symptr
  byte len, typelen, i

  len     = ^symstr
  typelen = SYM_TYPE | len
  len--; symstr++
  symptr  = sym_list
  while symptr
    if symptr->type == typelen
      for i = 0 to len
        if symptr->name[i] <> symstr->[i]; break; fin
      next
      if i > len
        return symptr
      fin
    fin
    symptr = symptr=>link
  loop
  return NULL
end

export def new_sym(symstr)#1
  var symptr

  symptr = match_sym(symstr)
  if symptr; return symptr; fin // Return already existing symbol
  symptr         = heapalloc(t_sym + ^symstr)
  symptr=>link   = sym_list
  sym_list       = symptr
  symptr->type   = ^symstr | SYM_TYPE
  symptr=>natv   = NULL
  symptr=>lambda = NULL
  symptr=>array  = NULL
  symptr=>apval  = NULL
  memcpy(symptr + name, symstr + 1, ^symstr)
  return symptr
end

//
// Print textual representation of S-expression
//

def print_atom(atom)#0
  char prstr[32]
  var elemptr, i, j, k, l

  if not atom
    puts("NIL")
  else
    when atom->type & TYPE_MASK
      is NIL
        puts(atom->type ?? "*T*" :: "NIL")
        break
      is NUM_TYPE
        when atom->type
          is NUM_INT
            if prhex
              putc('$')
              puth(atom=>intval[1]); puth(atom=>intval[0])
            else
              if atom=>intval[1] >= 0; putc(' '); fin // Add space for pos
              puti32(atom + intval)
            fin
            break
          is NUM_FLOAT
            puts(ext2str(atom + floatval, @prstr, fmt_fpint, fmt_fpfrac, fmt_fp))
            break
        wend
        break
      is SYM_TYPE
        prstr = atom->type & SYM_LEN
        memcpy(@prstr + 1, atom + name, prstr)
        puts(@prstr)
        break;
      is ARRAY_TYPE
        elemptr = atom=>arraymem
        puts("[ ")
        for i = 1 to atom=>dimension[0]
          if atom=>dimension[1]
            puts("\n[ ")
            for j = 1 to atom=>dimension[1]
              if atom=>dimension[2]
                puts("\n[ ")
                for k = 1 to atom=>dimension[2]
                  if atom=>dimension[3]
                    puts("\n[ ")
                    for l = 1 to atom=>dimension[3]
                      print_atom(*elemptr); putc(' ')
                      elemptr = elemptr + 2
                    next
                    puts("]")
                  else
                    print_atom(*elemptr); putc(' ')
                    elemptr = elemptr + 2
                  fin
                next
                puts("]")
              else
                print_atom(*elemptr); putc(' ')
                elemptr = elemptr + 2
              fin
            next
            puts("]")
          else
            print_atom(*elemptr); putc(' ')
            elemptr = elemptr + 2
          fin
        next
        puts("]\n")
        break
      otherwise
        puts("Unknown atom type: $"); putb(atom->type); putln
    wend
  fin
end

export def print_expr(expr)#0
  if not expr
    puts("NIL")
  else
    if expr->type == CONS_TYPE
      putc('(')
      while expr and expr->type == CONS_TYPE
        print_expr(expr=>car)
        expr = expr=>cdr
        if expr
          if expr->type <> CONS_TYPE
            putc('.')
            print_atom(expr)
            expr = NULL
          else
            putc(' ')
          fin
        fin
      loop
      putc(')')
    else
      print_atom(expr)
    fin
  fin
end

//
// Parse textual representation of S-expression
//

def is_num(cptr)
  if ^cptr == '-' or ^cptr == '+'; cptr++; fin
  if ^cptr == '$'
    cptr++
    if toupper(^cptr) >= 'A' and toupper(^cptr) <= 'F'
      return TRUE
    fin
  fin
  return ^cptr >= '0' and ^cptr <= '9'
end

def is_alphasym(c)
  return (c >= '*' and c <= 'z') and (c <> '.') and (c <> ',')
end

def parse_num(evalptr)#2 // return evalptr, intptr
  var startptr
  var int[2], ext[5]
  byte sign, h

  sign = FALSE
  if ^evalptr == '-'
    sign = TRUE
    evalptr++
  elsif ^evalptr == '+'
    evalptr++
  fin
  if ^evalptr == '$'
    evalptr++
    h = toupper(^evalptr)
    zero32
    while h >= '0' and h <= 'F'
      if h > '9'
        h = h - 'A' + 10
        if h > 16; break; fin
      else
        h = h - '0'
      fin
      muli16(16); addi16(h)
      evalptr++
      h = toupper(^evalptr)
    loop
  else
    startptr = evalptr
    while ^evalptr >= '0' and ^evalptr <= '9'
      evalptr++
    loop
    if (evalptr - startptr > 10) or ^evalptr == '.' or toupper(^evalptr) == 'E'
      if ^evalptr == '.'
        evalptr++
        while ^evalptr >= '0' and ^evalptr <= '9'
          evalptr++
        loop
      fin
      if toupper(^evalptr) == 'E'
        evalptr++
        if ^evalptr == '-' or ^evalptr == '+'; evalptr++; fin
        while ^evalptr >= '0' and ^evalptr <= '9'
          evalptr++
        loop
      fin
      if sign; startptr--; fin
      ^(startptr - 1) = evalptr - startptr
      str2ext(startptr - 1, @ext)
      return evalptr, new_float(@ext)
    fin
    zero32
    while startptr <> evalptr
      muli16(10); addi16(^startptr - '0')
      startptr++
    loop
  fin
  if sign; neg32; fin
  store32(@int)
  return evalptr, new_int(int[0], int[1])
end

def parse_sym(evalptr)#2 // return evalptr, symptr
  var symstr

  symstr = evalptr - 1
  while is_alphasym(^evalptr)
    ^evalptr = toupper(^evalptr)
    evalptr++
  loop
  ^symstr = evalptr - symstr - 1
  if ^symstr > 31; ^symstr = 31; fin
  return evalptr, new_sym(symstr)
end

export def parse_expr(evalptr, level, refill)#2 // return evalptr, exprptr
  var exprptr, consptr, elemptr, quotecons

  exprptr = NULL
  consptr = NULL
  while TRUE
    //
    // Parse textual S-expression
    //
    elemptr = NULL
    when ^evalptr
      is 0
        if level
          evalptr = refill() // Refill input buffer
        else
          return evalptr, exprptr
        fin
        break
      is ' '
      is ','
        evalptr++
        break
      is ')'
        if not exprptr
          exprptr = sym_nil
        fin
        return evalptr + 1, exprptr
      is '('
        evalptr++
        if level == 0
          level++
        else
          evalptr, elemptr = parse_expr(evalptr, 1, refill)
        fin
        break
      is '\''
        evalptr++
        evalptr, elemptr    = parse_expr(evalptr, 0, refill)
        quotecons           = new_cons
        quotecons=>car      = sym_quote
        quotecons=>cdr      = new_cons
        quotecons=>cdr=>car = elemptr
        elemptr             = quotecons
        if level == 0
          return evalptr, elemptr
        fin
        break
      is '.'
        evalptr++
        evalptr, elemptr = parse_expr(evalptr, 0, refill)
        //
        // Add expression to CDR
        //
        if not (consptr and consptr=>car)
          puts("Invalid . operator\n")
          return evalptr, exprptr
        fin
        consptr=>cdr = elemptr
        elemptr = NULL
        break
      otherwise
        if is_num(evalptr)
          evalptr, elemptr = parse_num(evalptr)
        elsif is_alphasym(^evalptr)
          evalptr, elemptr = parse_sym(evalptr)
        else
          putc('\\')
          putc(^evalptr)
          evalptr++
        fin
        if level == 0
          return evalptr, elemptr
        fin
    wend
    if elemptr
      //
      // Add element to S-expression
      //
      if not consptr
        consptr = new_cons
        exprptr = consptr
      else
        if consptr=>cdr
          puts("Improperly formed .\n")
          return evalptr, exprptr
        fin
        consptr=>cdr = new_cons
        consptr      = consptr=>cdr
      fin
      //
      // Add element to CAR
      //
      consptr=>car = elemptr
    fin
  loop
  return evalptr, exprptr
end

//
// Build/set association between symbols and values
//

export def new_assoc(symptr, valptr)#0
  var pair, pairlist

  if symptr and (symptr->type & TYPE_MASK <> SYM_TYPE)
    puts("Not a SYM in new_assoc\n")
    return
  fin
  pair          = new_cons
  pair=>car     = symptr
  pair=>cdr     = valptr
  pairlist      = new_cons
  pairlist=>car = pair
  pairlist=>cdr = assoc_list
  assoc_list    = pairlist
end

def assoc_pair(symptr)
  var pair

  //
  // Search local association list for symbol
  //
  pair = assoc_list
  while pair
    if (pair=>car=>car == symptr)
      return pair=>car
    fin
    pair = pair=>cdr
  loop
  return NULL // SYM not associated
end

export def set_assoc(symptr, valptr)#1
  var pair

  //
  // Search association list for symbol
  //
  pair = assoc_pair(symptr)
  if pair
    pair=>cdr = valptr // Update association
  fin
  return pair
end

def assoc(symptr)
  var pair

  //
  // Search local association list for symbol
  //
  pair = assoc_list
  while pair
    if (pair=>car=>car == symptr)
      return pair=>car=>cdr
    fin
    pair = pair=>cdr
  loop
  return NULL // SYM not associated
end

//
// Evaluate expression
//

def eval_args(argvals)
  var argstart

  //
  // Evaluate the argument parameters
  // - manipulate sweep_stack directly for performance
  //
  argstart = sweep_stack_top
  while argvals
    sweep_stack[sweep_stack_top] = eval_expr(argvals=>car)
    sweep_stack_top++
    if sweep_stack_top >= SWEEPSTACK_MAX
      puts("Arg val overflow\n")
      return NULL
    fin
    argvals = argvals=>cdr
  loop
  sweep_stack_top = argstart
  return sweep_stack_top
end

def apply_args(curl, expr, argvals)#2 // curl, expr
  var argsyms, arglist, pairlist, argbase

//  if expr and expr->type <> CONS_TYPE
//    if expr->type & TYPE_MASK == SYM_TYPE
//      if expr=>natv
//        return curl, expr=>natv(expr, argvals) // Native function
//      elsif expr=>lambda // DEFINEd lambda S-expression
//        expr = expr=>lambda
//      else
//        expr = assoc(expr)
//      fin
//    fin
//  fin
  if !expr or expr=>car <> sym_lambda
    puts("Invalid LAMBDA expression: "); print_expr(expr); putln
    return NULL, NULL
  fin
  argsyms = expr=>cdr=>car
  argbase = eval_args(argvals)
  if curl == expr
    //
    // Set associations
    //
    arglist = assoc_list
    while argsyms
      arglist=>car=>cdr = sweep_stack[argbase]
      arglist           = arglist=>cdr
      argsyms           = argsyms=>cdr
      argbase++
    loop
    if trace
      puts("TAIL call:")
    fin
  else
    //
    // Build arg list before prepending to assoc_list
    //
    arglist = NULL
    while argsyms
      //
      // Build argument/value pairs
      //
      if arglist
        pairlist=>cdr = new_cons
        pairlist      = pairlist=>cdr
      else
        arglist  = new_cons
        pairlist = arglist
      fin
      pairlist=>car      = new_cons
      pairlist=>car=>car = argsyms=>car
      pairlist=>car=>cdr = sweep_stack[argbase]
      argsyms            = argsyms=>cdr
      argbase++
    loop
    if arglist
      pairlist=>cdr = assoc_list
      assoc_list    = arglist
    fin
    if trace
      puts("APPLY call:")
    fin
  fin
  if trace
    print_expr(expr); putln
    print_expr(assoc_list); putln
  fin
  return expr, expr=>cdr=>cdr=>car
end

def eval_funarg(funarg, argvals)
  var funexpr, argsyms, arglist, pairlist, argbase

  funexpr = funarg=>cdr=>car // Lambda expression
  if funexpr->type <> CONS_TYPE
    if funexpr->type & TYPE_MASK == SYM_TYPE
      if funexpr=>natv
        return funexpr=>natv(funexpr, argvals) // Native function
      elsif funexpr=>lambda // DEFINEd lambda S-expression
        funexpr = funexpr=>lambda
      else
        funexpr = assoc(funexpr)
      fin
    fin
  fin
  if !funexpr or funexpr->type <> CONS_TYPE or funexpr=>car <> sym_lambda
    puts("Unknown FUNCTION:"); print_expr(funarg); putln
    return NULL
  fin
  //
  // Build arg list before prepending to new assoc_list
  //
  arglist = NULL
  argbase = eval_args(argvals)
  argsyms = funexpr=>cdr=>car
  while argsyms
    //
    // Build argument/value pairs
    //
    if arglist
      pairlist=>cdr = new_cons
      pairlist      = pairlist=>cdr
    else
      arglist  = new_cons
      pairlist = arglist
    fin
    pairlist=>car      = new_cons
    pairlist=>car=>car = argsyms=>car
    pairlist=>car=>cdr = sweep_stack[argbase]
    argsyms            = argsyms=>cdr
    argbase++
  loop
  push_sweep_stack(assoc_list)
  assoc_list = funarg=>cdr=>cdr=>car // Swap associcate list pointer
  if arglist
    pairlist=>cdr = assoc_list
    assoc_list    = arglist
  fin
  if trace
    puts("FUNARG call:"); print_expr(funarg); putln
    print_expr(assoc_list); putln
  fin
  funexpr               = eval_expr(funexpr=>cdr=>cdr=>car)
  funarg=>cdr=>cdr=>car = assoc_list // Save current environ
  assoc_list            = pop_sweep_stack
  return funexpr
end

export def eval_expr(expr)#1
  var alist_enter, curl, expr_car

  if gc_pull > GC_TRIGGER; gc; fin
  alist_enter = assoc_list
  curl        = NULL // Current lambda
  if trace
    puts("EVAL: "); print_expr(expr); putln
  fin
  while expr
    if expr->type == CONS_TYPE
      //
      // List - first element better be a function
      //
      expr_car = expr=>car
      if expr_car->type & TYPE_MASK == SYM_TYPE
        if expr_car=>natv
          expr = expr_car=>natv(expr_car, expr=>cdr) // Native function
          break
        elsif expr_car=>lambda // DEFINEd lambda S-expression
          curl, expr = apply_args(curl, expr_car=>lambda, expr=>cdr)
        elsif expr_car == sym_cond // Inline cond() evaluation
          expr = expr=>cdr
          while expr
            if eval_expr(expr=>car=>car)
              expr = expr=>car=>cdr=>car
              break
            fin
            expr = expr=>cdr
          loop
        elsif expr_car == sym_if // Inline if() evaluation
          expr = expr=>cdr
          if eval_expr(expr=>car)
            expr = expr=>cdr=>car // THEN clause
          else
            expr = expr=>cdr=>cdr
            if expr // Check for ELSE clause
              expr = expr=>car
            fin
          fin
        else // Associated symbol
          expr_car = assoc(expr_car)
        fin
      else
        curl = NULL // Set-up for in-line LAMBDA
      fin
      if !expr_car // Make sure we don't hang
        puts("Invalid function:"); print_expr(expr); putln
        expr = NULL
        break
      fin
      if expr_car->type == CONS_TYPE
        if expr_car=>car == sym_funarg // FUNARG
          expr = eval_funarg(expr_car, expr=>cdr)
          break
        fin
        if expr_car=>car == sym_label // LABEL
          new_assoc(expr_car=>cdr=>car, expr_car=>cdr=>cdr=>car) // Add LABEL
          expr_car = expr_car=>cdr=>cdr=>car  // Continue evaluating LAMBDA expression
        fin
        if expr_car=>car == sym_lambda // LAMBDA
          curl, expr = apply_args(curl, expr_car, expr=>cdr)
        else
          puts("LAMBDA expected:"); print_expr(expr); putln
          expr = NULL
          break
        fin
      fin
    else
      //
      // Atom - return the symbol value or the atom itself
      //
      if expr->type & TYPE_MASK == SYM_TYPE
        if expr=>apval // Constant
          expr = expr=>apval ^ NULL_HACK
        elsif expr=>lambda // DEFINEd lambda S-expression
          expr = expr=>lambda
        elsif expr=>array // Array
          expr = expr=>array
        else // Look on the association list last
          expr = assoc(expr)
        fin
      fin
      break
    fin
  loop
  assoc_list = alist_enter // Unwind assoc_list
  if trace
    puts("RESULT: "); print_expr(expr); putln
  fin
  return expr
end

export def eval_quote(expr)#1
  push_sweep_stack(assoc_list)
  assoc_list = NULL
  push_sweep_stack(expr) // Keep expr from being GC'ed
  expr = eval_expr(expr)
  pop_sweep_stack
  assoc_list = pop_sweep_stack
  return expr
end

//
// Make a copy of an expr
//

def copy_expr(expr)
  var copy

  if expr and expr->type == CONS_TYPE
    //
    // Copy cons structure
    //
    copy = push_sweep_stack(new_cons)
    while expr
      if expr=>car and expr=>car->type == CONS_TYPE
        copy=>car = copy_expr(expr=>car)
      else
        copy=>car = expr=>car
      fin
      if expr=>cdr and expr=>cdr->type == CONS_TYPE
        copy=>cdr = new_cons
        copy = copy=>cdr
        expr = expr=>cdr
      else // End of list
        copy=>cdr = expr=>cdr
        expr = NULL
      fin
    loop
    return pop_sweep_stack
  fin
  return expr
end
//
// Base native functions
//

export def bool_pred(bool)
  return bool ?? @pred_true :: NULL
end

def natv_atom(symptr, expr)
  symptr = eval_expr(expr=>car)
  return bool_pred(!symptr or symptr->type <> CONS_TYPE))
end

def natv_null(symptr, expr)
  return bool_pred(!eval_expr(expr=>car))
end

def natv_eq(symptr, expr)
  byte iseq, i
  var int[2], ext[5]

  iseq   = FALSE
  symptr = eval_expr(expr=>car)
  if symptr->type == NUM_INT
    int[0] = symptr=>intval[0]
    int[1] = symptr=>intval[1]
    expr   = eval_expr(expr=>cdr=>car)
    if expr->type == NUM_INT
      iseq = int[0] == expr=>intval[0] and int[1] == expr=>intval[1]
    fin
  elsif symptr->type == NUM_FLOAT
    ext[0] = symptr=>floatval[0]
    ext[1] = symptr=>floatval[1]
    ext[2] = symptr=>floatval[2]
    ext[3] = symptr=>floatval[3]
    ext[4] = symptr=>floatval[4]
    expr   = eval_expr(expr=>cdr=>car)
    if expr->type == NUM_FLOAT
      iseq = TRUE
      for i = 0 to 4
        if ext[i] <> expr=>floatval[i]
          iseq = FALSE
          break
        fin
      next
    fin
  else
    iseq = symptr == eval_expr(expr=>cdr=>car)
  fin
  return bool_pred(iseq)
end

def natv_and(symptr, expr)
  while expr and eval_expr(expr=>car)
    expr = expr=>cdr
  loop
  return bool_pred(!expr)
end

def natv_or(symptr, expr)
  while expr and !eval_expr(expr=>car)
    expr = expr=>cdr
  loop
  return bool_pred(expr)
end

def natv_cons(symptr, expr)
  symptr         = push_sweep_stack(new_cons)
  symptr=>car    = eval_expr(expr=>car)
  symptr=>cdr    = eval_expr(expr=>cdr=>car)
  return pop_sweep_stack
end

def natv_list(symptr, expr)
  if expr
    symptr = push_sweep_stack(new_cons)
    repeat
      symptr=>car = eval_expr(expr=>car)
      expr        = expr=>cdr
      if expr
        symptr=>cdr = new_cons
        symptr      = symptr=>cdr
      fin
    until !expr
    return pop_sweep_stack
  fin
  return NULL
end

def natv_car(symptr, expr)
  return eval_expr(expr=>car)=>car
end

def natv_cdr(symptr, expr)
  return eval_expr(expr=>car)=>cdr
end

def natv_quote(symptr, expr)
  return expr=>car
end

def natv_function(symptr, expr)
  var funptr

  funptr      = new_cons
  symptr      = funptr
  symptr=>car = sym_funarg
  symptr=>cdr = new_cons
  symptr      = symptr=>cdr
  symptr=>car = expr=>car
  symptr=>cdr = new_cons
  symptr      = symptr=>cdr
  symptr=>car = copy_expr(assoc_list)
  return funptr
end

def natv_define(symptr, expr)

  var deflist, funcptr

  deflist = NULL
  if expr
    deflist  = new_cons
    funcptr  = deflist
  fin
  while expr
    symptr         = expr=>car=>car
    symptr=>lambda = expr=>car=>cdr=>car
    funcptr=>car   = symptr
    expr           = expr=>cdr
    if expr
      funcptr=>cdr = new_cons
      funcptr      = funcptr=>cdr
    fin
  loop
  return deflist
end

def eval_index(arrayptr, expr)
  var idx[4], i, ii, index

  ii = 0
  while expr and ii < 4
    index = eval_expr(expr=>car)
    if index->type <> NUM_INT or isuge(index=>intval, arrayptr=>dimension[ii])
      puts("Invalid array index: "); print_expr(expr=>car); putln
      return NULL
    fin
    idx[ii] = index=>intval
    expr    = expr=>cdr
    ii++
  loop
  index = 0
  while ii
    ii--
    index = index + idx[ii] * arrayptr=>offset[ii]
  loop
  return arrayptr=>arraymem + index
end

def natv_index(symptr, expr)
  var elemptr

  if expr=>car == sym_set
    elemptr = eval_index(symptr=>array, expr=>cdr=>cdr)
    if elemptr; *elemptr = eval_expr(expr=>cdr=>car); fin
  else
    elemptr = eval_index(symptr=>array, expr)
  fin
  return elemptr ?? *elemptr :: NULL
end

def natv_array(symptr, expr)
  var arraylist, aptr
  var idx_expr, idx[4], ii, index

  arraylist = NULL
  if expr
    arraylist = new_cons
    aptr      = arraylist
  fin
  while expr
    symptr       = expr=>car=>car
    symptr=>natv = @natv_index
    idx_expr     = expr=>car=>cdr=>car
    idx[0]       = 0
    idx[1]       = 0
    idx[2]       = 0
    idx[3]       = 0
    ii           = 0
    while idx_expr and ii < 4
      index = eval_expr(idx_expr=>car)
      if index->type <> NUM_INT
        puts("Invalid array dimension\n"); print_expr(idx_expr=>car); putln
        return NULL
      fin
      idx[ii]  = index=>intval
      idx_expr = idx_expr=>cdr
      ii++
    loop
    symptr=>array = new_array(idx[0], idx[1], idx[2], idx[3])
    aptr=>car     = symptr
    expr          = expr=>cdr
    if expr
      aptr=>cdr = new_cons
      aptr      = aptr=>cdr
    fin
  loop
  return arraylist
end

def natv_cset(symptr, expr)
  symptr = eval_expr(expr=>car)
  if symptr->type & TYPE_MASK <> SYM_TYPE
    puts("CSET: Not a SYM\n")
    return NULL
  fin
  expr = eval_expr(expr=>cdr=>car)
  symptr=>apval = expr ^ NULL_HACK
  return expr
end

def natv_csetq(symptr, expr)
  symptr = expr=>car
  if symptr->type & TYPE_MASK <> SYM_TYPE
    puts("CSETQ: Not a SYM\n")
    return NULL
  fin
  expr = eval_expr(expr=>cdr=>car)
  symptr=>apval = expr ^ NULL_HACK
  return expr
end

def natv_pri(symptr, expr)
  var result

  result = NULL
  while expr
    if expr=>car == sym_space
      result = sym_space
      putc(' ')
    elsif expr=>car == sym_cr
      result = sym_cr
      putln
    else
      result = eval_expr(expr=>car)
      print_expr(result)
    fin
    expr = expr=>cdr
  loop
  return result
end

def natv_prhex(symptr, expr)
  if expr
    prhex = eval_expr(expr=>car) ?? TRUE :: FALSE
  fin
  return bool_pred(prhex)
end

def natv_print(symptr, expr)
  expr = natv_pri(symptr, expr)
  putln
  return expr
end

def natv_for(symptr, expr)
  var index, ufunc, dlist
  var[2] incval, stepval

  if expr=>car->type & TYPE_MASK <> SYM_TYPE
    puts("For index not symbol\n")
    return NULL
  fin
  index = eval_expr(expr=>car)
  expr  = expr=>cdr
  if index->type <> NUM_INT
    puts("FOR index not integer\n")
    return NULL
  fin
  symptr = eval_expr(expr=>car)
  expr   = expr=>cdr
  if symptr->type <> NUM_INT
    puts("FOR initial not integer\n")
    return NULL
  fin
  incval[0] = symptr=>intval[0]
  incval[1] = symptr=>intval[1]
  symptr = eval_expr(expr=>car)
  expr   = expr=>cdr
  if symptr->type <> NUM_INT
    puts("FOR step not integer\n")
    return NULL
  fin
  stepval[0] = symptr=>intval[0]
  stepval[1] = symptr=>intval[1]
  ufunc      = expr=>car
  dlist      = expr=>cdr
  //
  // Enter loop
  //
  push_sweep_stack(NULL)
  while eval_expr(ufunc)
    expr = dlist
    while expr
      //
      // Keep result from getting GC'ed
      //
      sweep_stack[sweep_stack_top - 1] = eval_expr(expr=>car)
      expr                             = expr=>cdr
    loop
    load32(index + intval) // Manipulate integer value directly!
    add32(@stepval)
    store32(index + intval)
  loop
  return pop_sweep_stack
end

def natv_while(symptr, expr)
  var ufunc, dlist

  ufunc = expr=>car
  dlist = expr=>cdr
  //
  // Enter loop
  //
  push_sweep_stack(NULL)
  while eval_expr(ufunc)
    expr = dlist
    while expr
      //
      // Keep result from getting GC'ed
      //
      sweep_stack[sweep_stack_top - 1] = eval_expr(expr=>car)
      expr                             = expr=>cdr
    loop
  loop
  return pop_sweep_stack
end

def natv_until(symptr, expr)
  var ufunc, dlist

  ufunc = expr=>car
  dlist = expr=>cdr
  //
  // Enter loop
  //
  push_sweep_stack(NULL)
  repeat
    expr = dlist
    while expr
      //
      // Keep result from getting GC'ed
      //
      sweep_stack[sweep_stack_top - 1] = eval_expr(expr=>car)
      expr                             = expr=>cdr
    loop
  until eval_expr(ufunc)
  return pop_sweep_stack
end

def natv_trace(symptr, expr)
  if expr
    trace = eval_expr(expr=>car) ?? TRUE :: FALSE
  fin
  return bool_pred(prhex)
end

//
// Install default functions
//

new_sym("T")=>apval       = @pred_true ^ NULL_HACK
new_sym("F")=>apval       = NULL_HACK
sym_space                 = new_sym("SPACE")
sym_cr                    = new_sym("CR")
sym_nil                   = new_sym("NIL")
sym_nil=>apval            = NULL_HACK
sym_lambda                = new_sym("LAMBDA")
sym_funarg                = new_sym("FUNARG")
sym_cond                  = new_sym("COND")
sym_if                    = new_sym("IF")
sym_set                   = new_sym("SET")
sym_label                 = new_sym("LABEL")
sym_quote                 = new_sym("QUOTE")
sym_quote=>natv           = @natv_quote
new_sym("CAR")=>natv      = @natv_car
new_sym("CDR")=>natv      = @natv_cdr
new_sym("CONS")=>natv     = @natv_cons
new_sym("LIST")=>natv     = @natv_list
new_sym("ATOM")=>natv     = @natv_atom
new_sym("EQ")=>natv       = @natv_eq
new_sym("NOT")=>natv      = @natv_null
new_sym("AND")=>natv      = @natv_and
new_sym("OR")=>natv       = @natv_or
new_sym("NULL")=>natv     = @natv_null
new_sym("FUNCTION")=>natv = @natv_function
new_sym("DEFINE")=>natv   = @natv_define
new_sym("ARRAY")=>natv    = @natv_array
new_sym("CSET")=>natv     = @natv_cset
new_sym("CSETQ")=>natv    = @natv_csetq
new_sym("PRI")=>natv      = @natv_pri
new_sym("PRHEX")=>natv    = @natv_prhex
new_sym("PRINT")=>natv    = @natv_print
new_sym("FOR")=>natv      = @natv_for
new_sym("WHILE")=>natv    = @natv_while
new_sym("UNTIL")=>natv    = @natv_until
new_sym("TRACE")=>natv    = @natv_trace
return modkeep | modinitkeep
done
