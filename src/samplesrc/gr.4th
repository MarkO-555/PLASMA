: GR ( set lores graphics mode )
  $FB40 0 0 0 0 CALL DROP
;
: TEXT
  $FB39 0 0 0 0 CALL DROP
;
: COLOR ( color )
  $F864 SWAP 0 0 0 CALL DROP
;
: PLOT ( Y X )
  $F800 ROT ROT 0 SWAP 0 CALL DROP
;

15 VARIABLE BALLCLR
10 VARIABLE BALLX
20 VARIABLE BALLY
10 VARIABLE OLDX
20 VARIABLE OLDY
1  VARIABLE INCX
1  VARIABLE INCY

: MOVEBALL
  BALLX @ 0=   IF INCX @ NEG INCX ! THEN
  BALLX @ 39 = IF INCX @ NEG INCX ! THEN
  BALLY @ 0=   IF INCY @ NEG INCY ! THEN
  BALLY @ 37 = IF INCY @ NEG INCY ! THEN
  INCX @ BALLX +!
  INCY @ BALLY +!
  BALLCLR @ COLOR
  BALLY @ BALLX @ PLOT
  0 COLOR
  OLDY @ OLDX @ PLOT
  BALLX @ OLDX !
  BALLY @ OLDY !
;

: BOUNCE
  GR
  BEGIN
    MOVEBALL
    ?TERMINAL
  UNTIL
  KEY
  TEXT
;
