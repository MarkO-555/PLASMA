SRC" plasma.4th"
SRC" conio.4th"
0 VARIABLE RESUMEXT
0 VARIABLE EXECXT

: RESUME> ; ( PLACE HOLDER TO RESUME EXECUTION )
' RESUME> RESUMEXT !

: ?EXEC ( F -- )
  NOT IF ( SKIP CODE IN BETWEEN ?EXEC AND RESUME> )
    1 >R
    BEGIN
      BL WORD FIND IF
        CASE
          RESUMEXT @ OF
            R> 1- ?DUP 0= IF ( EXIT IF FINAL RESUME> )
              DROP EXIT
            THEN
            >R
            ENDOF
          EXECXT @ OF ( CHECK FOR NESTED ?EXEC )
            R> 1+ >R
            ENDOF
        ENDCASE
      ELSE
        DROP
      THEN
    AGAIN
  THEN
;
' ?EXEC EXECXT !

: STRING CREATE 256 ALLOT DOES> ; ( JUST ALLOCATE THE BIGGEST STRING POSSIBLE )

: CONFIRM" ( -- F )
  COMPILE ."
  ."  (Y/N)"
  KEY CR TOUPPER CHAR Y =
;

: INSERT.FLOPPY"
  ." Insert "
  COMPILE ."
  ."  into floppy drive" CR
  ."  Press any key to continue" KEY DROP CR
;

STRING DEST
STRING FILELIST

0 0 40 24 VIEWPORT
HOME
8 SPACES INVERSETEXT
." PLASMA HD INSTALL STAGE 2"
NORMALTEXT CR CR
0 1 40 23 VIEWPORT

DEST GETPFX DROP

INSERT.FLOPPY" PLASMA.SYS"
FILELIST " -R /PLASMA.SYS/SYS " STRCPY DEST STRCAT
" COPY" SWAP LOADMOD

CONFIRM" Copy demos?"
?EXEC
  INSERT.FLOPPY" PLASMA.DEMOS"
  FILELIST " -R /PLASMA.DEMOS/DEMOS " STRCPY DEST STRCAT
  " COPY" SWAP LOADMOD
RESUME>

CONFIRM" Copy floating point libraries?"
?EXEC
  INSERT.FLOPPY" PLASMA.FPSOS"
  FILELIST " -R /PLASMA.FPSOS/SYS " STRCPY DEST STRCAT
  " COPY" SWAP LOADMOD
RESUME>

CONFIRM" Copy networking libraries?"
?EXEC
  INSERT.FLOPPY" PLASMA.INET"
  FILELIST " -R /PLASMA.INET/* " STRCPY DEST STRCAT
  " COPY" SWAP LOADMOD
RESUME>

CONFIRM" Copy build tools?"
?EXEC
  DEST " BLD" STRCAT DROP
  " NEWDIR" DEST LOADMOD
  INSERT.FLOPPY" PLASMA.BLD"
  FILELIST " /PLASMA.BLD/BLD/PLASM /PLASMA.BLD/BLD/CODEOPT " STRCPY
  DEST STRCAT
  " COPY" SWAP LOADMOD
  FILELIST " -R /PLASMA.BLD/BLD/INC " STRCPY DEST STRCAT
  " COPY" SWAP LOADMOD

  CONFIRM" Copy sample PLASMA code?"
  ?EXEC
    FILELIST " -R /PLASMA.BLD/BLD/SAMPLES " STRCPY DEST STRCAT
    " COPY" SWAP LOADMOD
  RESUME>

  CONFIRM" Copy sample FORTH scripts?"
  ?EXEC
  INSERT.FLOPPY" PLASMA.4TH"
    FILELIST " -R /PLASMA.4TH/BLD/SCRIPTS " STRCPY DEST STRCAT
    " COPY" SWAP LOADMOD
  RESUME>

RESUME>

FILELIST " AUTORUN HDINSTALL.4TH" STRCPY
" DEL" SWAP LOADMOD

0 0 40 24 VIEWPORT
HOME
." Done" CR
BYE
