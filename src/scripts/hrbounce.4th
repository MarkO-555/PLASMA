: RESERVEHGR2
  $6000 HERE - DUP
  0< ABORT" Can't reserve HGR2"
  ALLOT
;

RESERVEHGR2

: ?PLASMA
  " IFACE" FIND
  SWAP DROP
  0= IF
    " PLASMA.4TH" SRC
  THEN
;

?PLASMA ( Load PLASMA if not already )

: ?CONIO
  " CONIOAPI" FIND
  SWAP DROP
  0= IF
    " CONIO.4TH" SRC
  THEN
;

?CONIO ( Load CONIO if not already )

: ?HGRLIB
  " HGRLIB" FIND
  SWAP DROP
  0= IF
    " HGRLIB.4TH" SRC
  THEN
;

?HGRLIB ( Load GRLIB if not already )

 5 VARIABLE BALLCLR
10 VARIABLE BALLX
20 VARIABLE BALLY
10 VARIABLE OLDX
20 VARIABLE OLDY
 1 VARIABLE INCX
 1 VARIABLE INCY

: BEEP 30 10 TONE ;

: BOOP 10 30 TONE ;

: MOVEBALL
  BALLX @ 0=    IF INCX @ NEGATE INCX ! BEEP THEN
  BALLX @ 279 = IF INCX @ NEGATE INCX ! BEEP THEN
  BALLY @ 0=    IF INCY @ NEGATE INCY ! BOOP THEN
  BALLY @ 191 = IF INCY @ NEGATE INCY ! BOOP THEN
  INCX @ BALLX +!
  INCY @ BALLY +!
  BALLCLR @ HGRCOLOR
  BALLX @ BALLY @ HGRPLOT
  0 HGRCOLOR
  OLDX @ OLDY @ HGRPLOT
  BALLX @ OLDX !
  BALLY @ OLDY !
;

: BOUNCE
  2 HGRMODE    DROP
  1 HGRDRAWBUF DROP
  1 HGRSHOW    DROP
  BEGIN
    MOVEBALL
    ?TERMINAL
  UNTIL
  KEY
  0 HGRMODE DROP
;

BOUNCE
